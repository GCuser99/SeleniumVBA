[COMCreatable(True)]
[PredeclaredId(False)]
[Hidden(False)]
[ClassId("38ED0FFA-E3F3-41C4-B601-65630A6866B5")]
[InterfaceId("38ED0FFA-E3F3-41C4-B601-39CFF389CC81")]
[Description("This class is used to manage print settings for generating pdf files using PrintToPDF method of the WebDriver class")]
Public Class WebPrintSettings

    ' ==========================================================================
    ' SeleniumVBA v7.1
    '
    ' A Selenium wrapper for browser automation developed for MS Office VBA
    '
    ' https://github.com/GCuser99/SeleniumVBA/tree/main
    '
    ' Contact Info:
    '
    ' https://github.com/6DiegoDiego9
    ' https://github.com/GCUser99
    ' ==========================================================================
    ' MIT License
    '
    ' Copyright (c) 2023-2026, GCUser99 and 6DiegoDiego9 (https://github.com/GCuser99/SeleniumVBA)
    '
    ' Permission is hereby granted, free of charge, to any person obtaining a copy
    ' of this software and associated documentation files (the "Software"), to deal
    ' in the Software without restriction, including without limitation the rights
    ' to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    ' copies of the Software, and to permit persons to whom the Software is
    ' furnished to do so, subject to the following conditions:
    '
    ' The above copyright notice and this permission notice shall be included in all
    ' copies or substantial portions of the Software.
    '
    ' THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    ' IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    ' FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    ' AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    ' LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
    ' OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
    ' SOFTWARE.
    ' ==========================================================================

    Option Explicit

    Private mMarginBottom As Single
    Private mMarginTop As Single
    Private mMarginLeft As Single
    Private mMarginRight As Single
    Private mPageHeight  As Single
    Private mPageWidth  As Single
    Private mBackground As Boolean
    Private mOrientation  As svbaOrientation
    Private mPageRanges() As Variant
    Private mPrintScale  As Single
    Private mShrinkToFit As Boolean
    Private mUnits As svbaUnits
    
    'CDP extensions - Edge/Chrome only:
    Private mHeaderTemplate As String
    Private mFooterTemplate As String
    Private mDisplayHeaderFooter As Boolean
    Private mGenerateTaggedPDF As Boolean
    Private mGenerateDocumentOutline As Boolean

    Public Enum svbaOrientation
        svbaLandscape = 0
        svbaPortrait = 1
    End Enum

    Public Enum svbaUnits
        svbaCentimeters = 0
        svbaInches = 1
    End Enum

    [Description("Sets or gets the measurement units in centimeters or inches")]
    Public Property Get Units() As svbaUnits
        Units = mUnits
    End Property
    Public Property Let Units(ByVal val As svbaUnits)
        mUnits = val
    End Property

    [Description("Sets or gets the bottom margin")]
    Public Property Get MarginBottom() As Single
        MarginBottom = mMarginBottom
    End Property
    Public Property Let MarginBottom(ByVal val As Single)
        mMarginBottom = val
    End Property

    [Description("Sets or gets the top margin")]
    Public Property Get MarginTop() As Single
        MarginTop = mMarginTop
    End Property
    Public Property Let MarginTop(ByVal val As Single)
        mMarginTop = val
    End Property

    [Description("Sets or gets the left margin")]
    Public Property Get MarginLeft() As Single
        MarginLeft = mMarginLeft
    End Property
    Public Property Let MarginLeft(ByVal val As Single)
        mMarginLeft = val
    End Property

    [Description("Sets or gets the right margin")]
    Public Property Get MarginRight() As Single
        MarginRight = mMarginRight
    End Property
    Public Property Let MarginRight(ByVal val As Single)
        mMarginRight = val
    End Property

    [Description("Sets all margins to same value")]
    Public Property Let MarginsAll(ByVal val As Single)
        mMarginBottom = val
        mMarginTop = val
        mMarginLeft = val
        mMarginRight = val
    End Property

    [Description("Sets or gets the page height")]
    Public Property Get PageHeight() As Single
        PageHeight = mPageHeight
    End Property
    Public Property Let PageHeight(ByVal val As Single)
        mPageHeight = val
    End Property

    [Description("Sets or gets the page width")]
    Public Property Get PageWidth() As Single
        PageWidth = mPageWidth
    End Property
    Public Property Let PageWidth(ByVal val As Single)
        mPageWidth = val
    End Property

    [Description("Sets or gets the background property")]
    Public Property Get Background() As Boolean
        Background = mBackground
    End Property
    Public Property Let Background(ByVal val As Boolean)
        mBackground = val
    End Property

    [Description("Sets or gets the page orientation")]
    Public Property Get Orientation() As svbaOrientation
        Orientation = mOrientation
    End Property
    Public Property Let Orientation(ByVal val As svbaOrientation)
        mOrientation = val
    End Property

    [Description("Sets a list of pages and page ranges to print")]
    Public Sub PageRanges(ParamArray pages() As Variant)
        mPageRanges = pages
    End Sub

    [Description("Sets or gets the print scale (fractional)")]
    Public Property Get PrintScale() As Single
        PrintScale = mPrintScale
    End Property
    Public Property Let PrintScale(ByVal val As Single)
        mPrintScale = val
    End Property

    [Description("Sets or gets the shrink-to-fit property")]
    Public Property Get ShrinkToFit() As Boolean
        ShrinkToFit = mShrinkToFit
    End Property
    Public Property Let ShrinkToFit(ByVal val As Boolean)
        mShrinkToFit = val
    End Property
    
    [Description("Sets or gets whether to display Footer and Header")]
    Public Property Get DisplayHeaderFooter() As Boolean
        DisplayHeaderFooter = mDisplayHeaderFooter
    End Property
    Public Property Let DisplayHeaderFooter(ByVal val As Boolean)
        mDisplayHeaderFooter = val
    End Property
    
    [Description("Sets or gets the header template in HTML format")]
    Public Property Get HeaderTemplate() As String
        HeaderTemplate = mHeaderTemplate
    End Property
    Public Property Let HeaderTemplate(ByVal val As String)
        mHeaderTemplate = val
    End Property
    
    [Description("Sets or gets the footer template in HTML format")]
    Public Property Get FooterTemplate() As String
        FooterTemplate = mFooterTemplate
    End Property
    Public Property Let FooterTemplate(ByVal val As String)
        mFooterTemplate = val
    End Property
    
    [Description("Sets or gets whether to embed a document outline into the PDF")]
    Public Property Get GenerateDocumentOutline() As Boolean
        GenerateDocumentOutline = mGenerateDocumentOutline
    End Property
    Public Property Let GenerateDocumentOutline(ByVal val As Boolean)
        mGenerateDocumentOutline = val
    End Property
    
    [Description("Sets or gets whether to generate a tagged PDF")]
    Public Property Get GenerateTaggedPDF() As Boolean
        GenerateTaggedPDF = mGenerateTaggedPDF
    End Property
    Public Property Let GenerateTaggedPDF(ByVal val As Boolean)
        mGenerateTaggedPDF = val
    End Property

    Friend Function ToDictionary(browser_ As svbaBrowser) As Dictionary
        Dim margins As New Dictionary
        Dim page As New Dictionary
        Dim data As New Dictionary
        Dim i As Long
    
        Dim ufact As Single

        Select Case browser_
            Case Chrome, Edge
                'Note: CDP assumes incoming dimensions are in inches
                If mUnits = svbaInches Then ufact = 1 Else ufact = 2.54

                data.Add "marginTop", mMarginTop * ufact 'defaults to .4
                data.Add "marginBottom", mMarginBottom * ufact 'defaults to .4
                data.Add "marginLeft", mMarginLeft * ufact 'defaults to .4
                data.Add "marginRight", mMarginRight * ufact 'defaults to .4
    
                data.Add "paperWidth", mPageWidth * ufact 'defaults to 8.5
                data.Add "paperHeight", mPageHeight * ufact 'defaults to 11
    
                data.Add "printBackground", mBackground 'defaults to False
    
                If mOrientation = svbaPortrait Then
                    data.Add "landscape", False 'defaults to False
                Else
                    data.Add "landscape", True 'defaults to False
                End If
    
                Dim pageRangeString As String
        
                If UBound(mPageRanges) >= 0 Then
                    pageRangeString = CStr(mPageRanges(0))
                    For i = 1 To UBound(mPageRanges)
                        pageRangeString = pageRangeString & ", " & CStr(mPageRanges(i))
                    Next i
                End If
                data.Add "pageRanges", pageRangeString 'defaults to "" example'1-5, 8, 11-13'
                data.Add "scale", mPrintScale 'defaults to 1
                data.Add "preferCSSPageSize", Not mShrinkToFit 'defaults to False
                If mHeaderTemplate <> "" Or mFooterTemplate <> "" Then
                    data.Add "displayHeaderFooter", True 'defaults to False
                Else
                    data.Add "displayHeaderFooter", mDisplayHeaderFooter 'defaults to False
                End If
                data.Add "headerTemplate", mHeaderTemplate 'defaults to ""
                data.Add "footerTemplate", mFooterTemplate 'defaults to ""
                data.Add "transferMode", "ReturnAsBase64" 'defaults to ReturnAsStream
                data.Add "generateTaggedPDF", mGenerateTaggedPDF 'defaults to false
                data.Add "generateDocumentOutline", mGenerateDocumentOutline 'defaults to False
            Case Firefox
                'Note: Selenium WebDriver assumes incoming dimensions are in centimeters
                If mUnits = svbaInches Then ufact = 2.54 Else ufact = 1
                margins.Add "bottom", mMarginBottom * ufact
                margins.Add "left", mMarginLeft * ufact
                margins.Add "right", mMarginRight * ufact
                margins.Add "top", mMarginTop * ufact
    
                page.Add "height", mPageHeight * ufact
                page.Add "width", mPageWidth * ufact
    
                data.Add "background", mBackground
    
                If mOrientation = svbaPortrait Then
                    data.Add "orientation", "portrait"
                Else
                    data.Add "orientation", "landscape"
                End If
    
                For i = 0 To UBound(mPageRanges)
                    mPageRanges(i) = CStr(mPageRanges(i))
                Next i
    
                data.Add "margin", margins
                data.Add "page", page
                data.Add "pageRanges", mPageRanges 'eg. Array("1-2", "4-7", 13, "15")
                data.Add "scale", mPrintScale
                data.Add "shrinkToFit", mShrinkToFit
        End Select
    
        Set ToDictionary = data
    End Function

    Private Sub Class_Initialize()
        Dim iniFilePath As String
    
        'attempt to read global variable values from optional settings file - if not successful,
        'then assign a system default value (last input parameter of ReadIniFileEntry)
    
        iniFilePath = getFullLocalPath(thisLibFolderPath, , True) & "\SeleniumVBA.ini"
    
        mUnits = enumTextToValue(readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "units", svbaInches))
        mMarginBottom = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "margin_bottom", 1 / 2.54)
        mMarginTop = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "margin_top", 1 / 2.54)
        mMarginLeft = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "margin_left", 1 / 2.54)
        mMarginRight = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "margin_right", 1 / 2.54)
        mPageHeight = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "page_height", 11)
        mPageWidth = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "page_width", 8.5)
        mBackground = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "background", False)
        mOrientation = enumTextToValue(readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "orientation", svbaPortrait))
        mPageRanges = Array()
        mPrintScale = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "print_scale", 1)
        mShrinkToFit = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "shrink_to_fit", True)
        
        mHeaderTemplate = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "header_template", vbNullString)
        mFooterTemplate = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "footer_template", vbNullString)
        mDisplayHeaderFooter = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "display_header_footer", False)
        mGenerateTaggedPDF = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "generate_tagged_pdf", False)
        mGenerateDocumentOutline = readIniFileEntry(iniFilePath, "PDF_DEFAULT_PRINT_SETTINGS", "generate_document_outline", False)
    End Sub

End Class
